buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'me.tatarka.retrolambda'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2g"
    }

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")

        applicationId globalConfiguration.getAt("androidApplicationId")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        testInstrumentationRunner globalConfiguration.getAt("testInstrumentationRunner")
        testApplicationId globalConfiguration.getAt("testApplicationId")

        buildConfigField "String", "TRIBE_PUBLIC_KEY", "\"${tribe_public_key}\""
        buildConfigField "String", "TRIBE_PRIVATE_KEY", "\"${tribe_private_key}\""

        buildConfigField "String", "MARVEL_PUBLIC_KEY", "\"${marvel_public_key}\""
        buildConfigField "String", "MARVEL_PRIVATE_KEY", "\"${marvel_private_key}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            // Some libraries have issues with this.
        disable 'OldTargetApi'              // Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             // For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  // For testing purpose. This is safe to remove.
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies
    def developmentDependencies = rootProject.ext.developmentDependencies

    apt presentationDependencies.daggerCompiler
    compile presentationDependencies.dagger
    apt presentationDependencies.butterKnifeCompiler
    compile presentationDependencies.butterKnife
    compile presentationDependencies.recyclerView
    compile presentationDependencies.appCompat
    compile presentationDependencies.designSupport
    compile presentationDependencies.rxJava
    compile presentationDependencies.rxAndroid
    compile presentationDependencies.retrofit
    compile presentationDependencies.retrofitGsonConverter
    compile presentationDependencies.retrofitRxAdapter
    compile presentationDependencies.gson
    compile presentationDependencies.okHttp
    compile presentationDependencies.okHttpLoggingInterceptor
    compile presentationDependencies.androidAnnotations
    provided presentationDependencies.javaxAnnotation
    compile presentationDependencies.priorityJobQueue
    compile presentationDependencies.adapterDelegates
    compile presentationDependencies.pahoMQTT3
    compile presentationDependencies.rxBindings
    compile presentationDependencies.graphQL
    compile presentationDependencies.picasso
    compile presentationDependencies.picassoDownloader
    compile presentationDependencies.byteUnits

    androidTestCompile presentationTestDependencies.mockito
    androidTestCompile presentationTestDependencies.dexmaker
    androidTestCompile presentationTestDependencies.dexmakerMockito
    androidTestCompile presentationTestDependencies.espresso
    androidTestCompile presentationTestDependencies.androidAnnotations
    androidTestCompile presentationTestDependencies.testingSupportLib

    // Development
    compile developmentDependencies.leakCanary
    compile developmentDependencies.stetho
    compile developmentDependencies.stethoOkHttp
}
